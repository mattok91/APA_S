FUNCTION "fcCalculateSpeed" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      inTargetSpeed : Real;   // Setpoint prędkości [m/s]
      inAcceleration : Real;   // Przyspieszenie [m/s²]
      inDeceleration : Real;   // Opóźnienie [m/s²]
   END_VAR

   VAR_IN_OUT 
      ioCurrentSpeed : Real;   // Aktualna prędkość [m/s]
   END_VAR

   VAR_TEMP 
      tTime : Real;
   END_VAR


BEGIN
	(* Funkcja służy do obliczania aktualnej prędkośći przenośnika 
	wliczając w to wartości przyspieszania oraz opóźnienia*)
	
	//-- Konwersja do real
	#tTime := USINT_TO_REAL("CycleTimeData".PrevScanTime.Milliseconds) / 1000.0;
	
	IF #inTargetSpeed > #ioCurrentSpeed THEN
	    // przyspieszenie lub opoznienie na podstawie ostatniej predkosci
	    IF #ioCurrentSpeed >= 0 THEN
	        #ioCurrentSpeed += #inAcceleration * #tTime;
	    ELSIF #ioCurrentSpeed < 0 THEN
	        #ioCurrentSpeed += #inDeceleration * #tTime;
	    END_IF;
	    // overflow
	    IF #ioCurrentSpeed > #inTargetSpeed THEN
	        #ioCurrentSpeed := #inTargetSpeed;
	    END_IF;
	    
	ELSIF #inTargetSpeed < #ioCurrentSpeed THEN
	    // przyspieszenie lub opoznienie na podstawie ostatniej predkosci
	    IF #ioCurrentSpeed <= 0 THEN
	        #ioCurrentSpeed -= #inAcceleration * #tTime;
	    ELSIF #ioCurrentSpeed > 0 THEN
	        #ioCurrentSpeed -= #inDeceleration * #tTime;
	    END_IF;
	    // overflow
	    IF #ioCurrentSpeed < #inTargetSpeed THEN
	        #ioCurrentSpeed := #inTargetSpeed;
	    END_IF;
	END_IF;
	
END_FUNCTION

