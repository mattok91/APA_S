FUNCTION "fcTrackingReadID" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      inOffset : UInt;
      inClearID : Bool;
   END_VAR

   VAR_OUTPUT 
      ouIDPosEdge : UInt;
   END_VAR

   VAR_IN_OUT 
      ioArray : Array[*] of "typeTrackingStep";
      ioData : "typeTrackingData";
      ioIDOccupied : UInt;
   END_VAR

   VAR_TEMP 
      tQuotient : UInt;
      tReminder : UInt;
      tArrayLength : DInt;
      tFirstIndex : DInt;
      tInc : Int;
   END_VAR


BEGIN
	
	// określenie górny/dolny zakres tablicy
	#tArrayLength := UPPER_BOUND(ARR := #ioArray, DIM := 1);
	#tFirstIndex := LOWER_BOUND(ARR := #ioArray, DIM := 1);
	
	#tQuotient := #inOffset / #ioData.WindowLength;
	#tReminder := #inOffset MOD #ioData.WindowLength;
	
	//zerowanie ID
	#ouIDPosEdge := 0;
	
	IF #tQuotient >= #tFirstIndex AND #tQuotient <= #tArrayLength THEN
	    
	    //ID z trackingu danych w pozycji inOffset (posEdge)
	    IF #ioArray[#tQuotient].ID > 0 AND #ioArray[#tQuotient].Window > #tReminder THEN
	        IF #ioIDOccupied = 0 THEN
	            #ouIDPosEdge := #ioArray[#tQuotient].ID;
	        END_IF;
	        
	        #ioIDOccupied := #ioArray[#tQuotient].ID;
	        
	        //czy usunac ID z trackingu ?
	        IF #inClearID THEN
	            #ioArray[#tQuotient].ID := 0;
	            #ioArray[#tQuotient].Window := 0;
	        END_IF;
	    ELSE
	        #ioIDOccupied := 0;
	    END_IF;
	    
	END_IF;
END_FUNCTION

